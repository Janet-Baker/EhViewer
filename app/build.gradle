plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"
    ndkVersion "25.1.8937393"

    splits {
        abi {
            enable = true
            reset()
            include 'arm64-v8a', 'x86_64', 'armeabi-v7a', 'x86'
            universalApk = true
        }
    }

    signingConfigs {
        sign {
            storeFile file(signStoreFile)
            storePassword signStorePassword
            keyAlias signkeyAlias
            keyPassword signkeyPassword
            enableV3Signing = true
            enableV4Signing = true
        }
    }

    defaultConfig {
        applicationId "com.roblox.client"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 1411
        versionName "2.561.358"
        resConfigs 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW',  'ja'
    }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        abortOnError true
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "**.txt"
            excludes += "**.bin"
        }
    }

    dependenciesInfo.includeInApk false

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    namespace 'com.hippo.ehviewer'
}

dependencies {
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.core:core:1.10.0-alpha01'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation "androidx.paging:paging-runtime:3.2.0-alpha03"
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-rc01'
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'
    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.5.0.1'
    implementation 'dev.rikka.rikkax.core:core:1.4.1'
    //noinspection GradleDependency
    implementation 'dev.rikka.rikkax.material:material:1.6.6'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'org.jsoup:jsoup:1.15.3'
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}
